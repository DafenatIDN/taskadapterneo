apply plugin:'application'
mainClassName = "com.taskadapter.launcher.TALauncher"

ext {
    ZIP_FILE_NAME_PREFIX = "taskadapter"
    WAR_FILE_LOCATION = "../webui/build/libs"
    WAR_FILE_NAME = "webui.war"
}

distZip.dependsOn ":webui:war"

dependencies {
    def jetty_version = "9.3.6.v20151106"

    compile  (
            ["org.eclipse.jetty:jetty-server:$jetty_version"],
            ["org.eclipse.jetty:jetty-webapp:$jetty_version"]
    )

    compile "javax.servlet.jsp:jsp-api:2.1"
}

startScripts {
    ext.jvmOpts = "-Xmx512m"
    // "defaultJvmOpts" is a special variable recognized by "application" plugin.
    // it is used to set JVM options
    defaultJvmOpts = [ext.jvmOpts]
    inputs.property("jvmOpts", { ext.jvmOpts }) // for incremental build to work properly

    doLast {
        // add "--openTaskAdapterPageInWebBrowser" at the end of the command line parameters
        def WIN_REPLACE_ARG2 = "com.taskadapter.launcher.TALauncher %CMD_LINE_ARGS%"
        def WIN_REPLACE_ARG2_WITH = WIN_REPLACE_ARG2 + " --openTaskAdapterPageInWebBrowser"
        windowsScript.text = windowsScript.text.replace(WIN_REPLACE_ARG2, WIN_REPLACE_ARG2_WITH)

        def LIN_REPLACE_ARG2 = "com.taskadapter.launcher.TALauncher \"\$@\""
        def LIN_REPLACE_ARG2_WITH = LIN_REPLACE_ARG2 + " --openTaskAdapterPageInWebBrowser"
        unixScript.text = unixScript.text.replace(LIN_REPLACE_ARG2, LIN_REPLACE_ARG2_WITH)


        // add "export APP_HOME" to the linux script.
        // otherwise APP_HOME is not available at runtime (see Launcher.java).
        // see http://forums.gradle.org/gradle/topics/linux_launcher_script_should_export_app_home_before_starting_the_application
        def LINUX_START_TAG = "exec \"\$JAVACMD\" \"\${JVM_OPTS[@]}\""
        unixScript.text = unixScript.text.replace(LINUX_START_TAG, "\nexport APP_HOME\n" + LINUX_START_TAG)
    }
}

distZip {

    distZip.baseName = ZIP_FILE_NAME_PREFIX
    distZip.classifier = TASK_ADAPTER_VERSION

    def zipFileName = "$distZip.baseName-$distZip.classifier"

    into("$zipFileName/war") {
        from WAR_FILE_LOCATION
        include WAR_FILE_NAME
    }
}
