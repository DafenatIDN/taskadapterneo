ext {
    vaadinVersion = '7.7.10'
    httpClientVersion='4.2'
    slf4jVersion = '1.7.12'
    circeVersion = "0.9.3"

    WAR_FILE_NAME = "webui.war"
}

ext.libraries = [
    gson: 'com.google.code.gson:gson:1.6',
    libjson: 'net.liftweb:lift-json_2.12:3.1.0',
    httpclient: "org.apache.httpcomponents:httpclient:$httpClientVersion",
    httpcore: "org.apache.httpcomponents:httpcore:$httpClientVersion",
    servletAPI: "javax.servlet:javax.servlet-api:3.0.1",
    commonsCodec: "commons-codec:commons-codec:1.6",
    slf4j: "org.slf4j:slf4j-api:$slf4jVersion",
    slf4jBindingLibrary: "org.slf4j:slf4j-log4j12:$slf4jVersion",
    log4j: "log4j:log4j:1.2.17",
    mockitoCore: 'org.mockito:mockito-core:1.9.5-rc1',
    junit: "junit:junit:4.11",
    festassert: 'org.easytesting:fest-assert:1.4'
]

subprojects {

    apply plugin: 'scala' // it extends Java plugin, so you don't need to apply that one separately

    // java sources are added to Scala compile phase and removed from Java compile.
    // this solution is fix java->scala dependency issue
    // see  https://discuss.gradle.org/t/how-to-compile-a-java-class-that-depends-on-a-scala-class-in-gradle/6747
    sourceSets.main.scala.srcDir "src/main/java"
    sourceSets.main.java.srcDirs = []

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenLocal()
        maven {
            // note - cannot use https://dev.taskadapter.com/nexus/content/groups/public url for now
            // because Nginx->Nexus requires additional configuration for that.
            url 'http://dev.taskadapter.com:8081/nexus/repository/public'
        }
        maven {
            url 'http://maven.vaadin.com/vaadin-addons'
        }
        // This repository contains Atlassian JIRA REST Java Client library used by JIRA connector.
        // Browse artifacts online: https://maven.atlassian.com/content/groups/public/com/atlassian/jira/jira-rest-java-client-core/
        maven {
            url 'https://m2proxy.atlassian.com/repository/public'
        }
        mavenCentral()
    }

    dependencies {
        compile 'org.scala-lang:scala-library:2.12.5'

        compile 'com.google.guava:guava:19.0'
        testCompile libraries.junit
        testCompile 'org.scalatest:scalatest_2.12:3.0.0'
    }

    // disabled javadoc generation during Gradle build to avoid build failure.
    // see https://github.com/johndevs/gradle-vaadin-plugin/issues/130
    javadoc {
        enabled = false
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    test {
        testLogging {
            exceptionFormat = 'full'
        }
    }

    // this is a crutch for buggy Gradle 4.10.x - the build fails without it.
    configurations.all {
        if (name.startsWith("incrementalScalaAnalysis")) {
            extendsFrom = []
        }
    }
}


