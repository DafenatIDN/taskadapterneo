buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.devsoap.plugin:gradle-vaadin-plugin:1.2.0.beta1"
    }
}

apply plugin: 'war'
apply plugin: "com.devsoap.plugin.vaadin"

dependencies {
    compile 'javax.mail:mail:1.4.5'
    // this library is uploaded to our own Nexus repo because its Maven Central version is broken.
    compile 'com.taskadapter:googleanalyticstracker:2.2.0'

    // this Jar is actually added to the WAR file by the Vaadin Gradle plugin.
    // It is referenced here as a "runtime" dependency because otherwise IDEA does not copy
    // it to the dev output folder (classes/artifacts/webui/exploded/webui.war)
    runtime "com.vaadin:vaadin-themes:$vaadinVersion"

    compile libraries.slf4j
    compile libraries.libjson

    compile project(":webshared")

    compile "io.circe:circe-core_2.12:$circeVersion"
    compile "io.circe:circe-generic_2.12:$circeVersion"
    compile "io.circe:circe-parser_2.12:$circeVersion"

    compile (
            "org.scala-lang.modules:scala-parser-combinators_2.12:1.0.4",
            "com.rollbar:rollbar-java:1.6.0",
            "io.reactivex:rxscala_2.12:0.26.5"
    )

    compile project(":connectors-editors:basecamp-editor")
    compile project(":connectors-editors:basecamp-classic-editor")
    compile project(":connectors-editors:github-editor")
    compile project(":connectors-editors:jira-editor")
    compile project(":connectors-editors:mantis-editor")
    compile project(":connectors-editors:msp-editor")
    compile project(":connectors-editors:redmine-editor")
    compile project(":connectors-editors:editor-trello")

    testCompile project(":test-utilities")
    testCompile libraries.festassert
}

vaadin.mainSourceSet = sourceSets.main.allSource
vaadin {
    version = "$vaadinVersion"
}

war {
    // Exclude unnecessary GWT Compiler artifacts
    exclude "**/gwt-unitCache/**"

    // this file name is used by TALauncher script to init webapp context.
    // need to explicitly set it here, otherwise you get webui-projectversion.war file
    // while both TALauncher class and launcher/build.gradle expect "webui.war" without project version in the name
    archiveName "$WAR_FILE_NAME"
}

